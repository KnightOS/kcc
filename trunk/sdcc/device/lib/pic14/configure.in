#
# configure.in - input script to autoconf to
#                configure directory
#
# This file is part of the GNU PIC Library.
#
# Originally written by
# 	Vangelis Rokas <vrokas@otenet.gr>
#
# Adopted for the SDCC/pic14 library by
#	Raphael Neider <rneider AT web.de>
#
# $Id$
#

# Process this file with autoconf to produce a configure script.
AC_PREREQ(2.60)
AC_INIT([pic14lib], [0.1], [tecodev AT users sourceforge net])
AM_INIT_AUTOMAKE([foreign])
AC_CONFIG_SRCDIR([libsdcc/idata.c])
AC_CONFIG_HEADER([config.h])


NEWPICS=no;
AC_ARG_ENABLE(
    [new-pics],
    AS_HELP_STRING([--enable-new-pics], [Enable use of PIC devices added to gputils after the 0.13.7 release. Requires custom-built gputils obtained from their SVN repository!]),
    [ case "x$enableval" in
      xyes) NEWPICS=yes;
            AC_MSG_NOTICE([Enabling support for PICs from unreleased gputils -- make sure you have up-to-date gputils from SVN.]);;
      xno) NEWPICS=no;;
      *) AC_MSG_ERROR([bad value "${enableval}" for --enable-new-pics]);;
      esac],
    [ NEWPICS=no ]
)

AM_CONDITIONAL([HAVE_NEW_PICS], [ test x$NEWPICS = xyes ])

# SDCC setup
case $srcdir in
    [\\/]*|?:[\\/]*) abs_srcdir="$srcdir";
        ;;
    *) abs_srcdir="$ac_pwd/$srcdir";
        ;;
esac
case $ac_top_build_prefix in
    [\\/]*|?:[\\/]*)
        abs_top_builddir="$ac_top_build_prefix";
        ;;
    *) abs_top_builddir="$ac_pwd/$ac_top_build_prefix";
        ;;
esac

libdir=$libdir/pic14

# Checks for programs.
# The default architectures (regular and enhanced cores) can be selected at
# configure time by setting the environment variables ARCH and/or EARCH to
# the desired device (16fxxx).
AC_SUBST(ARCH,  [${ARCH:-16f877}])
AC_SUBST(EARCH, [${EARCH:-16f1934}])

# We cannot use AC_PROG_CC(sdcc) as sdcc might not be built at configure-time...
AC_SUBST(CC, [\'$abs_top_builddir/../../../bin/sdcc\'])
AC_PATH_PROG(CCAS, gpasm, :)
AC_PATH_PROG(LD, gplink, :)
AC_PATH_PROG(AR, gplib, :)

case ":$CCAS:$LD:$AR:" in
    *:::*)
        AC_MSG_ERROR([gputils (gpasm, gplink, and gplib) are required but not found.
Either install gputils or reconfigure with --disable-pic14-port and --disable-pic16-port.])
        ;;
esac;

AC_SUBST(CFLAGS, [])
AC_SUBST(CCASFLAGS, [])
AC_SUBST(LDFLAGS, ["-m -l -w"])
AC_SUBST(ARFLAGS, ["-c"])
AC_SUBST(CCAS, [\'$CCAS\'])
AC_SUBST(LD, [\'$LD\'])
AC_SUBST(AR, [\'$AR\'])

# $RANLIB is called by the autotools but not provided nor required
AC_SUBST(RANLIB, [:])
AC_SUBST(OBJEXT, [o])

_AM_DEPENDENCIES(CC)
_AM_DEPENDENCIES(CCAS)

# Checks for libraries.

# Checks for header files.

# Checks for typedefs, structures, and compiler characteristics.

# Checks for library functions.

AC_CONFIG_FILES([Makefile
                 libsdcc/enhanced/Makefile
                 libsdcc/regular/Makefile
		 libm/Makefile])
AC_OUTPUT
